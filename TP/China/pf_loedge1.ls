/PROG  PF_LOEDGE1
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 3733;
CREATE		= DATE 10-08-05  TIME 14:56:58;
MODIFIED	= DATE 18-06-27  TIME 21:04:24;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 174;
MEMORY_SIZE	= 4221;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
  ARC : TRUE ; 
  ARC Welding Equipment : 1,*,*,*,*;

MPAS ;
MPAS_NUM_PASSES        : 0;
MPAS_LAST_PASS         : 0;
MPAS_CURRENT_PASS      : 0;
MPAS_STATUS_PASS       : 0;
/MN
   1:  JMP LBL[555] ;
   2:  R[95]=(-1)    ;
   3:  //R[151]=0    ;
   4:  LBL[555] ;
   5:  !************************** ;
   6:  !** WELD V/U ;
   7:  !** Close Corner ;
   8:  !************************** ;
   9:   ;
  10:  UFRAME_NUM=2 ;
  11:  UTOOL_NUM=1 ;
  12:  Weave End ;
  13:  Weave End[7] ;
  14:  Track End ;
  15:   ;
  16:  !****************************** ;
  17:  !** I N I T I A L I S I N G  ** ;
  18:  !****************************** ;
  19:   ;
  20:   ;
  21:  R[114]=0    ;
  22:   ;
  23:   ;
  24:  !****************************** ;
  25:  !--   ANGLE CALC  -- ;
  26:  !****************************** ;
  27:  !* FUT 000 OR FUT 00-1 (PR[43-44) ;
  28:   ;
  29:  !***  POSITION CALC START ;
  30:  R[102]=(60+(R[95]*10)) ;
  31:   ;
  32:  !***  CONFIG CALC ;
  33:  R[103]=43    ;
  34:  R[103]=(45) ;
  35:  !** -------------------  ** ;
  36:   ;
  37:   ;
  38:   ;
  39:  !****************************** ;
  40:  !**** POSITION CALC ;
  41:  !****************************** ;
  42:   ;
  43:  !-- SAFE START  POINT ;
  44:  R[141]=(-50)    ;
  45:  R[142]=75    ;
  46:  CALL PRG_CALC1    ;
  47:  R[101]=R[102]+3    ;
  48:  PR[35]=PR[28]+PR[R[101]]    ;
  49:  PR[35,1]=PR[35,1]+R[141]    ;
  50:  PR[35,2]=(PR[35,2]+R[142]) ;
  51:  PR[35,3]=PR[35,3]+30    ;
  52:  PR[35,5]=60    ;
  53:   ;
  54:  !------- ;
  55:   ;
  56:  !--  WELDING  POINT 1 ;
  57:  R[141]=(0) ;
  58:  R[142]=(-4)    ;
  59:   ;
  60:  CALL PRG_CALC1    ;
  61:  R[101]=R[102]+3    ;
  62:  PR[1]=PR[28]+PR[R[101]]    ;
  63:  PR[1,1]=PR[1,1]+R[141]    ;
  64:  PR[1,2]=(PR[1,2]+R[142]) ;
  65:  PR[1,5]=87    ;
  66:  PR[1,3]=PR[1,3]-5    ;
  67:   ;
  68:  ! comparing with SP2 Z ;
  69:  R[101]=R[102]+2    ;
  70:  IF ((PR[1,3]-PR[R[101],3])<35),PR[1,5]=80 ;
  71:  !------- ;
  72:   ;
  73:  !-- WELDING  POINT 2 ;
  74:  R[101]=R[102]+3    ;
  75:  CALL PRG_CALC1    ;
  76:  PR[2]=PR[28]+PR[R[101]]    ;
  77:  PR[2,3]=PR[2,3]+0    ;
  78:  PR[2,5]=PR[1,5]    ;
  79:  !------- ;
  80:   ;
  81:  !-- WELDING  POINT 3 ;
  82:  PR[3]=PR[2]    ;
  83:  PR[3,3]=PR[2,3]+35    ;
  84:  PR[3,5]=90    ;
  85:  !------- ;
  86:   ;
  87:  !****************************** ;
  88:  !****    R E S U M I N G   **** ;
  89:  !****************************** ;
  90:   ;
  91:   ;
  92:  !-- AT RESUME JMP TO LABEL ;
  93:  IF R[151]>0,JMP LBL[R[151]] ;
  94:   ;
  95:  !ARCSENSOR START COUNT AT START ;
  96:  $TAST_PARAM[7].$SAMP_ST_Z=5 ;
  97:   ;
  98:  LBL[100] ;
  99:  R[151]=100    ;
 100:   ;
 101:  !****************************** ;
 102:  !**** INIT  POSITION ;
 103:  !****************************** ;
 104:   ;
 105:  PR[37]=JPOS    ;
 106:  PR[37,1]=0    ;
 107:  PR[37,2]=(-20)    ;
 108:  PR[37,3]=10    ;
 109:  PR[37,4]=0    ;
 110:  PR[37,5]=40    ;
 111:  PR[37,6]=0    ;
 112:   ;
 113:  //J PR[37] 60% CNT50    ;
 114:   ;
 115:  !****************************** ;
 116:   ;
 117:  LBL[11] ;
 118:  R[151]=11    ;
 119:   ;
 120:  !** -------------------  ** ;
 121:   ;
 122:  !-- MOVE TO SAFE START ;
 123:   ;
 124:L PR[35] 200mm/sec CNT50    ;
 125:   ;
 126:  !** -------------------  ** ;
 127:   ;
 128:  !-- MOVE TO WELD START ;
 129:  R[163]=1    ;
 130:  LBL[12] ;
 131:  R[151]=12    ;
 132:L PR[1] 150mm/sec FINE    ;
 133:   ;
 134:  !** -------------------  ** ;
 135:   ;
 136:   ;
 137:  !****************************** ;
 138:  !--   W E L D I N G  -- ;
 139:   ;
 140:  !-- 1. WELDING MOTION ;
 141:   ;
 142:  LBL[13] ;
 143:  R[151]=13    ;
 144:   ;
 145:   ;
 146:   ;
 147:   ;
 148:  Weave Sine[1] ;
 149:  Track TAST[R[21]] ;
 150:  Weld Start E1[1,R[111],E0] ;
 151:  WAIT   1.20(sec) ;
 152:  R[126]=R[126]*.8    ;
 153:L PR[2] R[126]cm/min CNT100    ;
 154:   ;
 155:  !---------------------------- ;
 156:   ;
 157:  !-- 2. WELDING MOTION ;
 158:   ;
 159:  LBL[14] ;
 160:  R[151]=14    ;
 161:   ;
 162:  Weave Sine[1] ;
 163:  Track TAST[R[21]] ;
 164:  Weld Start E1[1,R[111],E0] ;
 165:  R[126]=R[126]*.9    ;
 166:   ;
 167:L PR[3] R[126]cm/min CNT100    ;
 168:   ;
 169:  !---------------------------- ;
 170:   ;
 171:   ;
 172:  !****************************** ;
 173:  !--   E N D I N G    P R G.  -- ;
 174:   ;
/POS
/END
