/PROG  PB_EDGE1
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 4645;
CREATE		= DATE 10-08-05  TIME 14:56:58;
MODIFIED	= DATE 18-11-23  TIME 08:35:54;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 226;
MEMORY_SIZE	= 5149;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
  ARC : TRUE ; 
  ARC Welding Equipment : 1,*,*,*,*;

MPAS ;
MPAS_NUM_PASSES        : 0;
MPAS_LAST_PASS         : 0;
MPAS_CURRENT_PASS      : 0;
MPAS_STATUS_PASS       : 0;
/MN
   1:  JMP LBL[555] ;
   2:  R[151:TP JMP R1]=0    ;
   3:  LBL[555] ;
   4:  !************************** ;
   5:  !** Close edge ;
   6:  !************************** ;
   7:   ;
   8:  UFRAME_NUM=2 ;
   9:  UTOOL_NUM=1 ;
  10:  Weave End ;
  11:  Track End ;
  12:   ;
  13:   ;
  14:  !-- AT RESUME JMP TO LABEL ;
  15:  IF ((R[151:TP JMP R1]>0)),JMP LBL[R[151]] ;
  16:   ;
  17:   ;
  18:  !****************************** ;
  19:  !--   ANGLE CALC  -- ;
  20:  !****************************** ;
  21:  !* FUT 000 OR FUT 00-1 (PR[43-44) ;
  22:   ;
  23:  !***  POSITION CALC START ;
  24:  R[102:PosPtr L/R 1]=(60+(R[95:SE]*10)) ;
  25:   ;
  26:  !***  CONFIG CALC ;
  27:  R[103:Config L/R 1]=43    ;
  28:  IF (R[95:SE]=(1)),R[103:Config L/R 1]=(45) ;
  29:   ;
  30:  CALL PRG_CALC1    ;
  31:   ;
  32:  !**************************** ;
  33:  !**** CLOSE WEB ;
  34:  !**** POSITION CALC & WELDING ;
  35:  !**************************** ;
  36:   ;
  37:   ;
  38:  !-- SAFE START  POINT ;
  39:  !low angle ;
  40:  R[180:W. ang 1]=R[258:Low ang1]    ;
  41:  ! if normal angle and rat or no s ;
  42:  IF ((R[258:Low ang1]=0) AND ((R[70:TP step]=55) OR (R[257:web type1]=0) OR (R[288:Rat closing]=1) OR R[254:End type1]=3)) THEN ;
  43:  R[258:Low ang1]=0    ;
  44:  ELSE ;
  45:  R[258:Low ang1]=1    ;
  46:  ENDIF ;
  47:  R[185]=R[258:Low ang1]    ;
  48:   ;
  49:  ! changing angle for Side profile ;
  50:  IF (R[253:Flange1]=1) THEN ;
  51:  R[182:W. ang 3]=(-10)    ;
  52:  ENDIF ;
  53:   ;
  54:  !change angle ;
  55:  IF (((R[254:End type1]<>3) AND (R[254:End type1]<>7)) AND ((R[253:Flange1]=2) OR (R[495:projekt number]<>21226))) THEN ;
  56:  R[135:Calc free 1]=0    ;
  57:  R[181:W. ang 2]=5    ;
  58:  R[182:W. ang 3]=(-17)    ;
  59:  R[183:W. ang 4]=13    ;
  60:  ENDIF ;
  61:   ;
  62:   ;
  63:  R[141:X TP1]=0    ;
  64:  R[142:Y TP1]=20    ;
  65:  !Mirror if rat hole ;
  66:  IF (R[70:TP step]=55),R[141:X TP1]=R[141:X TP1]*(-1) ;
  67:  CALL PRG_CALC1    ;
  68:  R[101:Pos calc 1]=R[102:PosPtr L/R 1]+2    ;
  69:  PR[35:Start pos]=PR[21:DH nor]+PR[R[101]]    ;
  70:  PR[35,1:Start pos]=PR[35,1:Start pos]+R[141:X TP1]    ;
  71:  PR[35,2:Start pos]=PR[35,2:Start pos]+R[142:Y TP1]    ;
  72:  PR[35,3:Start pos]=PR[35,3:Start pos]+40    ;
  73:  !------- ;
  74:   ;
  75:  !--  WELDING  POINT 1 ;
  76:  !profile thickness ;
  77:  R[141:X TP1]=3.5    ;
  78:  R[142:Y TP1]=R[14:Profile Thicknes]/2*(-1)    ;
  79:  !Mirror if rat hole ;
  80:  IF (R[70:TP step]=55),R[141:X TP1]=R[141:X TP1]*(-1) ;
  81:  CALL PRG_CALC1    ;
  82:  R[101:Pos calc 1]=R[102:PosPtr L/R 1]+2    ;
  83:  PR[1:WP01]=PR[21:DH nor]+PR[R[101]]    ;
  84:  PR[1,1:WP01]=PR[1,1:WP01]+R[141:X TP1]    ;
  85:  PR[1,2:WP01]=PR[1,2:WP01]+R[142:Y TP1]    ;
  86:  PR[1,3:WP01]=PR[1,3:WP01]+3    ;
  87:  !------- ;
  88:   ;
  89:  !-- WELDING  POINT 2 ;
  90:  R[141:X TP1]=4.5    ;
  91:  R[142:Y TP1]=1.3    ;
  92:  !Mirror if rat hole ;
  93:  IF (R[70:TP step]=55),R[141:X TP1]=R[141:X TP1]*(-1) ;
  94:  CALL PRG_CALC1    ;
  95:  R[101:Pos calc 1]=R[102:PosPtr L/R 1]+2    ;
  96:  PR[2:WP02]=PR[21:DH nor]+PR[R[101]]    ;
  97:  PR[2,1:WP02]=PR[2,1:WP02]+R[141:X TP1]    ;
  98:  PR[2,2:WP02]=PR[2,2:WP02]+R[142:Y TP1]    ;
  99:  PR[2,3:WP02]=(PR[2,3:WP02]+0) ;
 100:  !------- ;
 101:   ;
 102:   ;
 103:  !-- WELDING  POINT 3 ;
 104:   ;
 105:  R[141:X TP1]=(-5)    ;
 106:  !skip edge ;
 107:  R[136:Calc free 1]=0    ;
 108:  IF (R[136:Calc free 1]=1),R[141:X TP1]=((-25)) ;
 109:  //ENDIF ;
 110:  R[142:Y TP1]=(-4)    ;
 111:  !Mirror if rat hole ;
 112:  IF (R[70:TP step]=55),R[141:X TP1]=R[141:X TP1]*(-1) ;
 113:  CALL PRG_CALC1    ;
 114:  R[101:Pos calc 1]=R[102:PosPtr L/R 1]+2    ;
 115:  PR[3:WP03]=PR[21:DH nor]+PR[R[101]]    ;
 116:  PR[3,1:WP03]=PR[3,1:WP03]+R[141:X TP1]    ;
 117:  PR[3,2:WP03]=PR[3,2:WP03]+R[142:Y TP1]    ;
 118:  PR[3,3:WP03]=PR[3,3:WP03]+0    ;
 119:   ;
 120:  !-- WELDING  POINT 8 ;
 121:  R[141:X TP1]=(-15)    ;
 122:  R[142:Y TP1]=(-1)    ;
 123:  !Mirror if rat hole ;
 124:  IF (R[70:TP step]=55),R[141:X TP1]=R[141:X TP1]*(-1) ;
 125:  CALL PRG_CALC1    ;
 126:  R[101:Pos calc 1]=R[102:PosPtr L/R 1]+2    ;
 127:  PR[8:WP08]=PR[21:DH nor]+PR[R[101]]    ;
 128:  PR[8,1:WP08]=PR[8,1:WP08]+R[141:X TP1]    ;
 129:  PR[8,2:WP08]=PR[8,2:WP08]+R[142:Y TP1]    ;
 130:   ;
 131:  !resetting angles ;
 132:  IF ((R[254:End type1]<>3) AND (R[254:End type1]<>7)) THEN ;
 133:  R[181:W. ang 2]=0    ;
 134:  R[182:W. ang 3]=(-8)    ;
 135:  R[183:W. ang 4]=5    ;
 136:  ENDIF ;
 137:  CALL PRG_CALC1    ;
 138:  R[181:W. ang 2]=0    ;
 139:  R[182:W. ang 3]=0    ;
 140:  R[183:W. ang 4]=0    ;
 141:  R[258:Low ang1]=R[180:W. ang 1]    ;
 142:  !---------------------------- ;
 143:  ! ***** WELDING ***** ;
 144:   ;
 145:  !-- MOVE TO SAFE START ;
 146:   ;
 147:  !skip edge ;
 148:  IF (R[136:Calc free 1]=1) THEN ;
 149:  IF (R[185]=0) THEN ;
 150:L PR[3:WP03] 150mm/sec CNT0    ;
 151:  ELSE ;
 152:L PR[3:WP03] 5.0sec CNT0    ;
 153:  ENDIF ;
 154:  JMP LBL[14] ;
 155:  ENDIF ;
 156:   ;
 157:  IF (R[185]=0) THEN ;
 158:L PR[35:Start pos] 350mm/sec CNT0    ;
 159:  ELSE ;
 160:L PR[35:Start pos] 5.0sec CNT0    ;
 161:  ENDIF ;
 162:   ;
 163:  !** -------------------  ** ;
 164:   ;
 165:  !-- MOVE TO WELD START ;
 166:  R[163:in seam R1]=1    ;
 167:   ;
 168:  LBL[11] ;
 169:  R[151:TP JMP R1]=11    ;
 170:   ;
 171:L PR[1:WP01] 150mm/sec FINE    ;
 172:  !Jump back to main program to sta ;
 173:  IF (R[296]=1) THEN ;
 174:  R[151:TP JMP R1]=20    ;
 175:  JMP LBL[1000] ;
 176:  ENDIF ;
 177:   ;
 178:   ;
 179:  !** -------------------  ** ;
 180:  LBL[20] ;
 181:   ;
 182:  LBL[12] ;
 183:  R[151:TP JMP R1]=12    ;
 184:  IF ((R[296]<>1)) THEN ;
 185:  Weld Start E1[1,10,E0] ;
 186:  ENDIF ;
 187:   ;
 188:  GO[3:E1 Memory Channel]=10 ;
 189:  WAIT    .50(sec) ;
 190:   ;
 191:L PR[2:WP02] R[126:WS_Calc R1]cm/min FINE    ;
 192:  WAIT    .50(sec) ;
 193:  !---------------------------- ;
 194:   ;
 195:  LBL[13] ;
 196:  R[151:TP JMP R1]=13    ;
 197:   ;
 198:   ;
 199:  GO[3:E1 Memory Channel]=10 ;
 200:   ;
 201:  IF ((R[296]<>1)) THEN ;
 202:  Weld Start E1[1,10,E0] ;
 203:  ENDIF ;
 204:   ;
 205:L PR[3:WP03] R[126:WS_Calc R1]cm/min FINE    ;
 206:  WAIT   0.00(sec) ;
 207:  GO[3:E1 Memory Channel]=R[111:E1 Schedule] ;
 208:   ;
 209:   ;
 210:  LBL[14] ;
 211:  R[151:TP JMP R1]=14    ;
 212:   ;
 213:  IF ((R[296]<>1)) THEN ;
 214:  Weld Start E1[1,R[111],E0] ;
 215:  ENDIF ;
 216:   ;
 217:  Weave Sine[1] ;
 218:  Track TAST[R[21]] ;
 219:   ;
 220:L PR[8:WP08] R[126:WS_Calc R1]cm/min FINE    ;
 221:   ;
 222:  LBL[999] ;
 223:   ;
 224:  //R[151:TP JMP R1]=0    ;
 225:   ;
 226:  LBL[1000] ;
/POS
/END
