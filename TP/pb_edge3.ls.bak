/PROG  PB_EDGE3
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 4423;
CREATE		= DATE 17-06-07  TIME 11:03:12;
MODIFIED	= DATE 18-02-21  TIME 09:44:26;
FILE_NAME	= PB_EDGE1;
VERSION		= 0;
LINE_COUNT	= 206;
MEMORY_SIZE	= 4879;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= *,*,1,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
  ARC : TRUE ; 
  ARC Welding Equipment : *,1,*,*,*;

MPAS ;
MPAS_NUM_PASSES        : 0;
MPAS_LAST_PASS         : 0;
MPAS_CURRENT_PASS      : 0;
MPAS_STATUS_PASS       : 0;
/MN
   1:  JMP LBL[555] ;
   2:  R[153]=0    ;
   3:  LBL[555] ;
   4:  !************************** ;
   5:  !** Close edge ;
   6:  !************************** ;
   7:   ;
   8:  UFRAME_NUM=2 ;
   9:  UTOOL_NUM=1 ;
  10:  Weave End ;
  11:  Track End ;
  12:   ;
  13:   ;
  14:  !-- AT RESUME JMP TO LABEL ;
  15:  IF ((R[153]>0)),JMP LBL[R[153]] ;
  16:   ;
  17:   ;
  18:  !Handling Rat hole points ;
  19:  IF (R[70]=55) THEN ;
  20:  PR[52]=PR[61]    ;
  21:  PR[15]=PR[11]    ;
  22:  ENDIF ;
  23:   ;
  24:  !****************************** ;
  25:  !--   ANGLE CALC  -- ;
  26:  !****************************** ;
  27:  !* FUT 000 OR FUT 00-1 (PR[43-44) ;
  28:   ;
  29:  !***  POSITION CALC START ;
  30:  R[105]=(60+(R[95]*10)) ;
  31:   ;
  32:  !***  CONFIG CALC ;
  33:  R[106]=43    ;
  34:  IF (R[95]=(1)),R[106]=(45) ;
  35:   ;
  36:  CALL PRG_CALC3    ;
  37:   ;
  38:  !**************************** ;
  39:  !**** CLOSE WEB ;
  40:  !**** POSITION CALC & WELDING ;
  41:  !**************************** ;
  42:   ;
  43:   ;
  44:  !-- SAFE START  POINT ;
  45:  !low angle ;
  46:  R[190]=R[268]    ;
  47:  ! if normal angle and rat or no s ;
  48:  IF ((R[268]=0) AND ((R[70]=55) OR (R[267]=0) OR (R[288]=1) OR R[264]=3)) THEN ;
  49:  R[268]=0    ;
  50:  ELSE ;
  51:  R[268]=1    ;
  52:  ENDIF ;
  53:  R[195]=R[268]    ;
  54:   ;
  55:  !change angle ;
  56:  R[138]=0    ;
  59:  R[191]=5    ;
  60:  R[192]=(-17)    ;
  61:  R[193]=13    ;
  62:   ;
  63:  R[145]=0    ;
  64:  R[146]=50    ;
  65:  !Mirror if rat hole ;
  66:  IF (R[70]=55),R[145]=R[145]*(-1) ;
  67:  CALL PRG_CALC3    ;
  68:  R[104]=R[105]+2    ;
  69:  PR[35]=PR[21]+PR[R[104]]    ;
  70:  PR[35,1]=PR[35,1]+R[145]    ;
  71:  PR[35,2]=PR[35,2]+R[146]    ;
  72:  PR[35,3]=PR[35,3]+20    ;
  73:  !------- ;
  74:   ;
  75:  !--  WELDING  POINT 1 ;
  76:  !profile thickness ;
  77:  R[145]=4.2    ;
  78:  R[146]=R[14]/1.8*(-1)    ;
  79:  !Mirror if rat hole ;
  80:  IF (R[70]=55),R[145]=R[145]*(-1) ;
  81:  CALL PRG_CALC3    ;
  82:  R[104]=R[105]+2    ;
  83:  PR[1]=PR[21]+PR[R[104]]    ;
  84:  PR[1,1]=PR[1,1]+R[145]    ;
  85:  PR[1,2]=PR[1,2]+R[146]    ;
  86:  PR[1,3]=PR[1,3]+3    ;
  87:  !------- ;
  88:   ;
  89:  !-- WELDING  POINT 2 ;
  90:  R[145]=4    ;
  91:  R[146]=(-.5)    ;
  92:  !Mirror if rat hole ;
  93:  IF (R[70]=55),R[145]=R[145]*(-1) ;
  94:  CALL PRG_CALC3    ;
  95:  R[104]=R[105]+2    ;
  96:  PR[2]=PR[21]+PR[R[104]]    ;
  97:  PR[2,1]=PR[2,1]+R[145]    ;
  98:  PR[2,2]=PR[2,2]+R[146]    ;
  99:  PR[2,3]=(PR[2,3]+0) ;
 100:  !------- ;
 101:   ;
 102:  !-- WELDING  POINT 3 ;
 103:  R[145]=(-6)    ;
 104:  !skip edge ;
 105:  R[139]=0    ;
 108:  IF (R[139]=1),R[145]=((-25)) ;
  113:  R[146]=0    ;
 114:  !Mirror if rat hole ;
 115:  IF (R[70]=55),R[145]=R[145]*(-1) ;
 116:  CALL PRG_CALC3    ;
 117:  PR[3]=PR[2]    ;
 118:  PR[3,1]=PR[3,1]+R[145]    ;
 119:  PR[3,2]=PR[3,2]+R[146]    ;
 120:   ;
 121:  !resetting angles ;
 122:  R[191]=0    ;
 123:  R[192]=(-8)    ;
 124:  R[193]=5    ;
 125:  CALL PRG_CALC3    ;
 126:  R[191]=0    ;
 127:  R[192]=0    ;
 128:  R[193]=0    ;
 129:  R[268]=R[190]    ;
 130:  !---------------------------- ;
 131:  ! ***** WELDING ***** ;
 132:   ;
 133:  !-- MOVE TO SAFE START ;
 134:  !skip edge ;
 135:  IF (R[139]=1) THEN ;
 136:  IF (R[195]=0) THEN ;
 137:L PR[3] 150mm/sec CNT50    ;
 138:  ELSE ;
 139:L PR[3] 3.0sec CNT50    ;
 140:  ENDIF ;
 141:  JMP LBL[14] ;
 142:  ENDIF ;
 143:   ;
 144:   ;
 145:  ! wait for robot 1 has moved away ;
 146:  IF (R[95]=1 AND R[290]=0) THEN ;
 147:  WAIT (R[221]=23 OR R[221]=30)    ;
 148:  IF (R[221]=23) THEN ;
 149:  WAIT (R[75]=1 OR R[221]<>23)    ;
 150:  ENDIF ;
 151:  IF (R[195]=0) THEN ;
 152:L PR[35] 150mm/sec CNT50    ;
 153:  ELSE ;
 154:L PR[35] 3.0sec CNT50    ;
 155:  ENDIF ;
 156:  WAIT (R[468]*(-1)>R[93] OR R[221]<>23)    ;
 157:  ELSE ;
 158:  IF (R[190]=0) THEN ;
 159:L PR[35] 150mm/sec CNT50    ;
 160:  ELSE ;
 161:L PR[35] 3.0sec CNT50    ;
 162:  ENDIF ;
 163:  ENDIF ;
 164:   ;
 165:  !** -------------------  ** ;
 166:   ;
 167:  !-- MOVE TO WELD START ;
 168:  R[164]=1    ;
 169:   ;
 170:  LBL[11] ;
 171:  R[153]=11    ;
 172:   ;
 173:L PR[1] 150mm/sec FINE    ;
 174:   ;
 175:  !** -------------------  ** ;
 176:   ;
 177:  LBL[12] ;
 178:  R[153]=12    ;
 179:  Weld Start E2[1,10,E0] ;
 180:   ;
 181:  !Set low current seq ;
 182:  GO[13]=10 ;
 183:  WAIT   1.00(sec) ;
 184:   ;
 185:L PR[2] R[127]cm/min FINE    ;
 186:   ;
 187:  !---------------------------- ;
 188:  LBL[13] ;
 189:  R[153]=13    ;
 190:   ;
 191:  GO[13]=10 ;
 192:  Weld Start E2[1,10,E0] ;
 193:   ;
 194:L PR[3] R[127]cm/min FINE    ;
 195:  GO[13]=R[112] ;
 196:   ;
 197:  LBL[14] ;
 198:  R[153]=14    ;
 199:  LBL[999] ;
 200:   ;
 201:  !Handling Rat hole points ;
 202:  IF (R[70]=55) THEN ;
 203:  PR[52]=PR[62]    ;
 204:  ENDIF ;
 205:  R[153]=0    ;
 206:   ;
/POS
/END
