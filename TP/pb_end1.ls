/PROG  PB_END1
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 5403;
CREATE		= DATE 10-08-05  TIME 14:56:58;
MODIFIED	= DATE 18-10-17  TIME 19:25:00;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 248;
MEMORY_SIZE	= 5859;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
  ARC : TRUE ; 
  ARC Welding Equipment : 1,*,*,*,*;

MPAS ;
MPAS_NUM_PASSES        : 0;
MPAS_LAST_PASS         : 0;
MPAS_CURRENT_PASS      : 0;
MPAS_STATUS_PASS       : 0;
/MN
   1:  JMP LBL[555] ;
   2:  R[151]=0    ;
   3:  LBL[555] ;
   4:  !************************** ;
   5:  !** WELD V/U Robot1 ;
   6:  !************************** ;
   7:   ;
   8:  !skip end of T-bar slopes ;
   9:  IF ((R[289]=1) AND (R[95]=1)) THEN ;
  10:  WAIT   2.00(sec) ;
  11:  JMP LBL[999] ;
  12:  ENDIF ;
  13:   ;
  14:  UFRAME_NUM=2 ;
  15:  UTOOL_NUM=1 ;
  16:  Weave End ;
  17:  Track End ;
  18:   ;
  19:   ;
  20:  !-- ARC PARAM -- ;
  21:  WAIT R[109]=0    ;
  22:  R[13]=6    ;
  23:  R[115]=1    ;
  24:  R[116]=1    ;
  25:  CALL WELD_PA    ;
  26:   ;
  27:  !-- AT RESUME JMP TO LABEL ;
  28:  IF ((R[151]=12)),JMP LBL[R[151]] ;
  29:  IF ((R[151]>100)) THEN ;
  30:   ;
  31:  !gap detect ;
  32:  IF (R[480]=5),R[480]=(0) ;
  33:   ;
  34:  IF (R[480]=0 AND R[480]<>1234 AND DO[95]=ON) THEN ;
  35:  R[480]=1    ;
  36:  RUN CURCHK ;
  37:  WAIT R[480]=2    ;
  38:  ENDIF ;
  39:  ENDIF ;
  40:   ;
  41:  IF ((R[151]>100)),JMP LBL[R[151]] ;
  42:   ;
  43:   ;
  44:  !**************************** ;
  45:  !**** POSITION CALC & POINTS ;
  46:  !**************************** ;
  47:   ;
  48:  !* FUT 000 OR FUT 00-1 (PR[43-44) ;
  49:   ;
  50:  !***  POSITION CALC START ;
  51:  R[102]=(60+(R[95]*10)) ;
  52:   ;
  53:  !***  CONFIG CALC ;
  54:  R[103]=43    ;
  55:  IF (R[95]=(1)),R[103]=(45) ;
  56:   ;
  57:  CALL PRG_CALC1    ;
  58:   ;
  59:   ;
  60:  !Determind sensing point to use ;
  61:  R[101]=R[102]+1    ;
  62:  IF (R[254]=2),R[101]=R[102]+2 ;
  63:  IF (R[254]=3),R[101]=R[102]+2 ;
  64:  IF (R[254]=4),R[101]=R[102]+2 ;
  65:  IF (R[254]=7),R[101]=R[102]+2 ;
  66:   ;
  67:   ;
  68:  !Start Angle up point ;
  69:  !-- WELDING  POINT 5 ;
  70:  !Depending on OProfile flange ;
  71:  R[135]=(-20)    ;
  72:  IF (R[253]=2),R[135]=(-50) ;
  73:  R[141]=R[135]    ;
  74:  R[142]=(0) ;
  75:  CALL PRG_CALC1    ;
  76:  PR[5]=PR[R[101]]    ;
  77:  PR[5,1]=PR[5,1]+R[141]    ;
  78:  PR[5,2]=PR[5,2]+R[142]    ;
  79:   ;
  80:  !End Angle up point ;
  81:  ! also start R3 point ;
  82:  !-- WELDING  POINT 6 ;
  83:  R[135]=R[135]-100    ;
  84:  R[141]=R[135]    ;
  85:  R[142]=(0) ;
  86:  CALL PRG_CALC1    ;
  87:  PR[6]=PR[21]+PR[R[101]]    ;
  88:  PR[6,1]=PR[6,1]+R[141]    ;
  89:  PR[6,2]=PR[6,2]+R[142]    ;
  90:   ;
  91:  !Move ahead point ;
  92:  !-- WELDING  POINT 7 ;
  93:  R[141]=((150)*(-1)) ;
  94:  ! Y-direction ;
  95:  IF (R[81]=2),R[141]=(200*(-1)) ;
  96:  R[142]=(0) ;
  97:  CALL PRG_CALC1    ;
  98:  PR[7]=PR[6]    ;
  99:  PR[7,1]=PR[7,1]+R[141]    ;
 100:  PR[7,2]=PR[7,2]+R[142]    ;
 101:  ! checking limit ;
 102:  IF (PR[7,1]<(-200)),PR[7,1]=(-200) ;
 103:   ;
 104:  !**************************** ;
 105:  ! Call sub programs for Starting ;
 106:  !**************************** ;
 107:  LBL[12] ;
 108:   ;
 109:  IF (R[254]=(1)) THEN ;
 110:  CALL PB_COR1    ;
 111:  IF R[151]=12,JMP LBL[1000] ;
 112:  ENDIF ;
 113:   ;
 114:  IF (R[254]=(2) OR R[254]=3 OR R[254]=4 OR R[254]=7) THEN ;
 115:  CALL PB_EDGE1    ;
 116:  IF R[151]=12,JMP LBL[1000] ;
 117:  ENDIF ;
 118:   ;
 119:  IF (R[254]=5 OR R[254]=6 OR R[254]=8) THEN ;
 120:  CALL PB_MID1    ;
 121:  R[151]=105    ;
 122:  !if start of profile jump to end ;
 123:  IF (R[221]=18 OR (R[221]=22 AND R[81]=1)),JMP LBL[1000] ;
 124:  ENDIF ;
 125:   ;
 126:  !gap detect ;
 127:  IF (R[480]=5),R[480]=(0) ;
 128:   ;
 129:  IF (R[480]=0 AND R[480]<>1234 AND DO[95]=ON) THEN ;
 130:  R[480]=1    ;
 131:  RUN CURCHK ;
 132:  WAIT R[480]=2    ;
 133:  ENDIF ;
 134:   ;
 135:  !Signal to start R3 ;
 136:  R[75]=1    ;
 137:   ;
 138:  ! if one robot welds both sides ;
 139:  IF (R[290]<>0),JMP LBL[115] ;
 140:   ;
 141:   ;
 142:  ! If not start of profile, jump t ;
 143:  IF (((R[95]=1) AND (R[81]=2)) OR (R[70]=55)),JMP LBL[115] ;
 144:   ;
 145:   ;
 146:  !**************************** ;
 147:  ! ***** WELDING ***** ;
 148:  ! ***** Sub points ***** ;
 149:  !**************************** ;
 150:   ;
 151:  !---------------------------- ;
 152:  !Start Angle up point ;
 153:  !-- WELDING MOTION P5 ;
 154:  LBL[105] ;
 155:  R[151]=105    ;
 156:  !Signal to start R3 ;
 157:  R[75]=1    ;
 158:   ;
 159:  !using current angles ;
 160:  PR[120]=LPOS    ;
 161:  PR[5,4]=PR[120,4]    ;
 162:  PR[5,5]=PR[120,5]    ;
 163:  PR[5,6]=PR[120,6]    ;
 164:   ;
 165:  !use different angles for endtyp ;
 166:  IF (R[254]=2 OR R[254]=4) THEN ;
 167:  PR[5,4]=PR[21,4]    ;
 168:  PR[5,5]=PR[21,5]    ;
 169:  PR[5,6]=PR[21,6]    ;
 170:  CALL PRG_CALC1    ;
 171:  ENDIF ;
 172:   ;
 173:  Weave Sine[1] ;
 174:  Track TAST[R[21]] ;
 175:  //Weld Start E1[1,R[111],E0] ;
 176:   ;
 177:  SKIP CONDITION R[75]=3    ;
 178:L PR[5] R[126]cm/min CNT100 Skip,LBL[1051]    ;
 179:  JMP LBL[999] ;
 180:  LBL[1051] ;
 181:   ;
 182:   ;
 183:  !---------------------------- ;
 184:  !-- WELDING MOTION P6 ;
 185:  !End Angle up point ;
 186:  LBL[106] ;
 187:  R[151]=106    ;
 188:  Weave Sine[1] ;
 189:  Track TAST[R[21]] ;
 190:  //Weld Start E1[1,R[111],E0] ;
 191:   ;
 192:  SKIP CONDITION R[75]=3    ;
 193:L PR[6] R[126]cm/min CNT100 Skip,LBL[1061]    ;
 194:  JMP LBL[999] ;
 195:  LBL[1061] ;
 196:   ;
 197:   ;
 198:  ! if one robot welds both sides ;
 199:  IF (R[290]<>0),JMP LBL[115] ;
 200:   ;
 201:   ;
 202:  !---------------------------- ;
 203:  !-- WELDING MOTION P7 ;
 204:  !Move ahead point ;
 205:  LBL[107] ;
 206:  R[151]=107    ;
 207:  Weave Sine[1] ;
 208:  Track TAST[R[21]] ;
 209:  //Weld Start E1[1,R[111],E0] ;
 210:   ;
 211:  SKIP CONDITION R[75]=3    ;
 212:L PR[7] R[126]cm/min CNT100 Skip,LBL[999]    ;
 213:  JMP LBL[999] ;
 214:   ;
 215:  ! If End of profile  or rathole ;
 216:  ! Weld back to close point ;
 217:  LBL[115] ;
 218:  R[151]=115    ;
 219:  ! go in UF1 ;
 220:  UFRAME_NUM=1 ;
 221:   ;
 222:  !Signal to start R3 ;
 223:  R[75]=1    ;
 224:   ;
 225:  PR[130]=LPOS    ;
 226:  PR[15,7]=PR[130,7]    ;
 227:   ;
 228:  R[101]=R[102]+15    ;
 229:  PR[15]=PR[R[101]]    ;
 230:   ;
 231:  CALL PRG_CALC1    ;
 232:  PR[15,4]=PR[21,4]    ;
 233:  PR[15,5]=PR[21,5]    ;
 234:  PR[15,6]=PR[21,6]    ;
 235:   ;
 236:  Weave Sine[1] ;
 237:  Track TAST[R[21]] ;
 238:  Weld Start E1[1,R[111],E0] ;
 239:L PR[15] R[126]cm/min FINE    ;
 240:  Weave End[1] ;
 241:  Track End ;
 242:  WAIT    .50(sec) ;
 243:  Weld End E1[1,R[111]] ;
 244:   ;
 245:  LBL[999] ;
 246:   ;
 247:   ;
 248:  LBL[1000] ;
/POS
/END
