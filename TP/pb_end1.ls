/PROG  PB_END1
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 6539;
CREATE		= DATE 10-08-05  TIME 14:56:58;
MODIFIED	= DATE 18-11-23  TIME 08:36:14;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 306;
MEMORY_SIZE	= 7027;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
  ARC : TRUE ; 
  ARC Welding Equipment : 1,*,*,*,*;

MPAS ;
MPAS_NUM_PASSES        : 0;
MPAS_LAST_PASS         : 0;
MPAS_CURRENT_PASS      : 0;
MPAS_STATUS_PASS       : 0;
/MN
   1:  JMP LBL[555] ;
   2:  R[151:TP JMP R1]=0    ;
   3:  LBL[555] ;
   4:  !************************** ;
   5:  !** WELD V/U Robot1 ;
   6:  !************************** ;
   7:   ;
   8:  !skip end of T-bar slopes ;
   9:  IF ((R[289:T-bar slope]=1) AND (R[95:SE]=1)) THEN ;
  10:  WAIT   2.00(sec) ;
  11:  JMP LBL[998] ;
  12:  ENDIF ;
  13:   ;
  14:  UFRAME_NUM=2 ;
  15:  UTOOL_NUM=1 ;
  16:  Weave End ;
  17:  Track End ;
  18:   ;
  19:   ;
  20:  !-- ARC PARAM -- ;
  21:  WAIT R[109:Weld_pa]=0    ;
  22:  R[13:Scale reg]=6    ;
  23:  R[115:Weld Para Num]=1    ;
  24:  R[116:C:\Users\RAFA\Dr]=1    ;
  25:  CALL WELD_PA    ;
  26:   ;
  27:  !-- AT RESUME JMP TO LABEL ;
  28:  IF ((R[151:TP JMP R1]=12)),JMP LBL[R[151]] ;
  29:  IF ((R[151:TP JMP R1]>100)) THEN ;
  30:   ;
  31:  !gap detect ;
  32:  IF (R[480:strom ana]=5),R[480:strom ana]=(0) ;
  33:   ;
  34:  IF (R[480:strom ana]=0 AND R[480:strom ana]<>1234 AND DO[95:Weld enable]=ON) THEN ;
  35:  R[480:strom ana]=1    ;
  36:  RUN CURCHK ;
  37:  WAIT R[480:strom ana]=2    ;
  38:  ENDIF ;
  39:  ENDIF ;
  40:   ;
  41:  IF ((R[151:TP JMP R1]>100)),JMP LBL[R[151]] ;
  42:   ;
  43:   ;
  44:  !**************************** ;
  45:  !**** POSITION CALC & POINTS ;
  46:  !**************************** ;
  47:   ;
  48:  !* FUT 000 OR FUT 00-1 (PR[43-44) ;
  49:   ;
  50:  !***  POSITION CALC START ;
  51:  R[102:PosPtr L/R 1]=(60+(R[95:SE]*10)) ;
  52:   ;
  53:  !***  CONFIG CALC ;
  54:  R[103:Config L/R 1]=43    ;
  55:  IF (R[95:SE]=(1)),R[103:Config L/R 1]=(45) ;
  56:   ;
  57:  CALL PRG_CALC1    ;
  58:   ;
  59:   ;
  60:  !Determind sensing point to use ;
  61:  R[101:Pos calc 1]=R[102:PosPtr L/R 1]+1    ;
  62:  IF (R[254:End type1]=2),R[101:Pos calc 1]=R[102:PosPtr L/R 1]+2 ;
  63:  IF (R[254:End type1]=3),R[101:Pos calc 1]=R[102:PosPtr L/R 1]+2 ;
  64:  IF (R[254:End type1]=4),R[101:Pos calc 1]=R[102:PosPtr L/R 1]+2 ;
  65:  IF (R[254:End type1]=7),R[101:Pos calc 1]=R[102:PosPtr L/R 1]+2 ;
  66:   ;
  67:   ;
  68:  !Start Angle up point ;
  69:  !-- WELDING  POINT 5 ;
  70:  !Depending on OProfile flange ;
  71:  R[135:Calc free 1]=(-20)    ;
  72:  IF (R[253:Flange1]=2),R[135:Calc free 1]=(-50) ;
  73:  R[141:X TP1]=R[135:Calc free 1]    ;
  74:  R[142:Y TP1]=(0) ;
  75:  CALL PRG_CALC1    ;
  76:  PR[5:WP05]=PR[R[101]]    ;
  77:  PR[5,1:WP05]=PR[5,1:WP05]+R[141:X TP1]    ;
  78:  PR[5,2:WP05]=PR[5,2:WP05]+R[142:Y TP1]    ;
  79:   ;
  80:  !End Angle up point ;
  81:  ! also start R3 point ;
  82:  !-- WELDING  POINT 6 ;
  83:  R[135:Calc free 1]=R[135:Calc free 1]-50    ;
  84:  R[141:X TP1]=R[135:Calc free 1]    ;
  85:  R[142:Y TP1]=(0) ;
  86:  CALL PRG_CALC1    ;
  87:  PR[6:WP06]=PR[21:DH nor]+PR[R[101]]    ;
  88:  PR[6,1:WP06]=PR[6,1:WP06]+R[141:X TP1]    ;
  89:  PR[6,2:WP06]=PR[6,2:WP06]+R[142:Y TP1]    ;
  90:   ;
  91:  !Move ahead point ;
  92:  !-- WELDING  POINT 7 ;
  93:  R[141:X TP1]=((150)*(-1)) ;
  94:  ! Y-direction ;
  95:  IF (R[81:X/Y retning]=2),R[141:X TP1]=(200*(-1)) ;
  96:  R[142:Y TP1]=(0) ;
  97:  CALL PRG_CALC1    ;
  98:  PR[7:WP07]=PR[6:WP06]    ;
  99:  PR[7,1:WP07]=PR[7,1:WP07]+R[141:X TP1]    ;
 100:  PR[7,2:WP07]=PR[7,2:WP07]+R[142:Y TP1]    ;
 101:  ! checking limit ;
 102:  IF (PR[7,1:WP07]<(-150)),PR[7,1:WP07]=(-150) ;
 103:   ;
 104:  !**************************** ;
 105:  ! Call sub programs for Starting ;
 106:  !**************************** ;
 107:  LBL[12] ;
 108:   ;
 109:  IF (R[254:End type1]=(1)) THEN ;
 110:  CALL PB_COR1    ;
 111:  ! if welding is controlled by mai ;
 112:  IF R[151:TP JMP R1]=20,JMP LBL[1000] ;
 113:  ENDIF ;
 114:   ;
 115:  IF (R[254:End type1]=(2) OR R[254:End type1]=4) THEN ;
 116:  CALL PB_EDGE1    ;
 117:  ! if welding is controlled by mai ;
 118:  IF R[151:TP JMP R1]=20,JMP LBL[1000] ;
 119:  ENDIF ;
 120:   ;
 121:  IF (R[254:End type1]=3 OR R[254:End type1]=7) THEN ;
 122:  CALL PB_EDGEF1    ;
 123:  ! if welding is controlled by mai ;
 124:  IF R[151:TP JMP R1]=20,JMP LBL[1000] ;
 125:  ENDIF ;
 126:   ;
 127:  IF (R[254:End type1]=5 OR R[254:End type1]=6 OR R[254:End type1]=8) THEN ;
 128:  CALL PB_MID1    ;
 129:  ! if welding is controlled by mai ;
 130:  R[151:TP JMP R1]=101    ;
 131:  !if sync welding ;
 132:  IF (R[296]=1),JMP LBL[1000] ;
 133:  ENDIF ;
 134:   ;
 135:  !gap detect ;
 136:  IF (R[480:strom ana]=5),R[480:strom ana]=(0) ;
 137:   ;
 138:  IF (R[480:strom ana]=0 AND R[480:strom ana]<>1234 AND DO[95:Weld enable]=ON) THEN ;
 139:  R[480:strom ana]=1    ;
 140:  RUN CURCHK ;
 141:  WAIT R[480:strom ana]=2    ;
 142:  ENDIF ;
 143:   ;
 144:  LBL[101] ;
 145:  !Signal to start R3 ;
 146:  IF (R[70:TP step]<>22),R[75:Robot done]=1 ;
 147:   ;
 148:  ! if straigthen up gun ;
 149:  IF (R[297]=1),JMP LBL[104] ;
 150:   ;
 151:  ! no straigth up ;
 152:  JMP LBL[107] ;
 153:   ;
 154:   ;
 155:  !**************************** ;
 156:  ! ***** WELDING ***** ;
 157:  ! ***** Sub points ***** ;
 158:  !**************************** ;
 159:   ;
 160:  LBL[104] ;
 161:  ! Checking on total welding lengt ;
 162:  ! depending on profile dir ;
 163:  ! log in UF 1 ;
 164:  ! only for no sync welding ;
 165:  IF (R[296]=0) THEN ;
 166:  UFRAME_NUM=1 ;
 167:  PR[120:used in fold etc]=LPOS    ;
 168:  IF (R[81:X/Y retning]=1) THEN ;
 169:  R[134:Calc free 1]=PR[120,1:used in fold etc]-PR[15,1:weld back]    ;
 170:  ELSE ;
 171:  R[134:Calc free 1]=PR[120,2:used in fold etc]-PR[15,2:weld back]    ;
 172:  ENDIF ;
 173:  IF (R[134:Calc free 1]<0),R[134:Calc free 1]=R[134:Calc free 1]*(-1) ;
 174:  ! jmp to last point ;
 175:  IF R[134:Calc free 1]<150,JMP LBL[115] ;
 176:  ENDIF ;
 177:   ;
 178:  ! back in UF 2 ;
 179:  UFRAME_NUM=2 ;
 180:   ;
 181:  !---------------------------- ;
 182:  !Start Angle up point ;
 183:  !-- WELDING MOTION P5 ;
 184:  LBL[105] ;
 185:  R[151:TP JMP R1]=105    ;
 186:  !Signal to start R3 ;
 187:  IF (R[70:TP step]<>22),R[75:Robot done]=1 ;
 188:   ;
 189:  !using current angles ;
 190:  PR[120:used in fold etc]=LPOS    ;
 191:  PR[5,4:WP05]=PR[120,4:used in fold etc]    ;
 192:  PR[5,5:WP05]=PR[120,5:used in fold etc]    ;
 193:  PR[5,6:WP05]=PR[120,6:used in fold etc]    ;
 194:   ;
 195:   ;
 196:  Weave Sine[1] ;
 197:  Track TAST[R[21]] ;
 198:  IF ((R[296]<>1)) THEN ;
 199:  Weld Start E1[1,R[111],E0] ;
 200:  ENDIF ;
 201:   ;
 202:  SKIP CONDITION R[75:Robot done]=3    ;
 203:L PR[5:WP05] R[126:WS_Calc R1]cm/min CNT100 Skip,LBL[1051]    ;
 204:  JMP LBL[998] ;
 205:  LBL[1051] ;
 206:   ;
 207:   ;
 208:  !---------------------------- ;
 209:  !-- WELDING MOTION P6 ;
 210:  !End Angle up point ;
 211:  LBL[106] ;
 212:  R[151:TP JMP R1]=106    ;
 213:  Weave Sine[1] ;
 214:  Track TAST[R[21]] ;
 215:  IF ((R[296]<>1)) THEN ;
 216:  Weld Start E1[1,R[111],E0] ;
 217:  ENDIF ;
 218:   ;
 219:  SKIP CONDITION R[75:Robot done]=3    ;
 220:L PR[6:WP06] R[126:WS_Calc R1]cm/min CNT100 Skip,LBL[1061]    ;
 221:  JMP LBL[998] ;
 222:  LBL[1061] ;
 223:   ;
 224:   ;
 225:   ;
 226:   ;
 227:  !---------------------------- ;
 228:  !-- WELDING MOTION P7 ;
 229:  !Move ahead point ;
 230:  LBL[107] ;
 231:  R[151:TP JMP R1]=107    ;
 232:   ;
 233:  ! no sync welding ;
 234:  IF ((R[296]<>0)),JMP LBL[115] ;
 235:   ;
 236:  Weave Sine[1] ;
 237:  Track TAST[R[21]] ;
 238:  IF ((R[296]<>1)) THEN ;
 239:  Weld Start E1[1,R[111],E0] ;
 240:  ENDIF ;
 241:   ;
 242:  SKIP CONDITION R[75:Robot done]=3    ;
 243:L PR[7:WP07] R[126:WS_Calc R1]cm/min CNT100 Skip,LBL[998]    ;
 244:  JMP LBL[998] ;
 245:   ;
 246:  ! If End of profile  or rathole ;
 247:  ! Weld back to close point ;
 248:  LBL[115] ;
 249:  R[151:TP JMP R1]=115    ;
 250:  ! go in UF1 ;
 251:  UFRAME_NUM=1 ;
 252:   ;
 253:  !Signal to start R3 ;
 254:  R[75:Robot done]=1    ;
 255:   ;
 256:  PR[130]=LPOS    ;
 257:  PR[15,7:weld back]=PR[130,7]    ;
 258:   ;
 259:  R[101:Pos calc 1]=R[102:PosPtr L/R 1]+15    ;
 260:  PR[15:weld back]=PR[R[101]]    ;
 261:   ;
 262:  CALL PRG_CALC1    ;
 263:  PR[15,4:weld back]=PR[21,4:DH nor]    ;
 264:  PR[15,5:weld back]=PR[21,5:DH nor]    ;
 265:  PR[15,6:weld back]=PR[21,6:DH nor]    ;
 266:   ;
 267:  ! if weld back with gantry ;
 268:  IF ((R[296]=3)),JMP LBL[1000] ;
 269:   ;
 270:  ! adjust 7 axis position ;
 271:  IF ((R[81:X/Y retning]=1) AND (PR[15,1:weld back]>100)) THEN ;
 272:  IF (PR[15,7:weld back]<PR[15,2:weld back]) THEN ;
 273:  PR[15,7:weld back]=(PR[15,2:weld back]-PR[15,1:weld back]/1) ;
 274:  ELSE ;
 275:  PR[15,7:weld back]=(PR[15,2:weld back]+PR[15,1:weld back]/1) ;
 276:  ENDIF ;
 277:  ENDIF ;
 278:   ;
 279:  Weave Sine[1] ;
 280:  Track TAST[R[21]] ;
 281:  Weld Start E1[1,R[111],E0] ;
 282:L PR[15:weld back] R[126:WS_Calc R1]cm/min FINE    ;
 283:  Weave End[1] ;
 284:  Track End ;
 285:  WAIT    .50(sec) ;
 286:  Weld End E1[1,R[111]] ;
 287:  ! safe point ;
 288:  IF (R[81:X/Y retning]=1) THEN ;
 289:  UFRAME_NUM=2 ;
 290:  R[141:X TP1]=0    ;
 291:  R[142:Y TP1]=100    ;
 292:  CALL PRG_CALC1    ;
 293:  PR[35:Start pos]=PR[6:WP06]    ;
 294:  PR[35,1:Start pos]=PR[35,1:Start pos]+R[141:X TP1]    ;
 295:  PR[35,2:Start pos]=PR[35,2:Start pos]+R[142:Y TP1]    ;
 296:  PR[35,3:Start pos]=PR[35,3:Start pos]+30    ;
 297:   ;
 298:L PR[35:Start pos] 150mm/sec FINE    ;
 299:  ENDIF ;
 300:   ;
 301:  LBL[998] ;
 302:  ! wait register ;
 303:  R[294]=2    ;
 304:   ;
 305:   ;
 306:  LBL[1000] ;
/POS
/END
