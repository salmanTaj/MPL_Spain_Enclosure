/PROG  PB_END1
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 5271;
CREATE		= DATE 10-08-05  TIME 14:56:58;
MODIFIED	= DATE 18-03-13  TIME 06:38:30;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 239;
MEMORY_SIZE	= 5755;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
  ARC : TRUE ; 
  ARC Welding Equipment : 1,*,*,*,*;

MPAS ;
MPAS_NUM_PASSES        : 0;
MPAS_LAST_PASS         : 0;
MPAS_CURRENT_PASS      : 0;
MPAS_STATUS_PASS       : 0;
/MN
   1:  JMP LBL[555] ;
   2:  R[151]=0    ;
   3:  LBL[555] ;
   4:  !************************** ;
   5:  !** WELD V/U Robot1 ;
   6:  !************************** ;
   7:   ;
   8:  !skip end of T-bar slopes ;
   9:  IF ((R[289]=1) AND (R[95]=1)) THEN ;
  10:  WAIT   2.00(sec) ;
  11:  JMP LBL[999] ;
  12:  ENDIF ;
  13:   ;
  14:  UFRAME_NUM=2 ;
  15:  UTOOL_NUM=1 ;
  16:  Weave End ;
  17:  Track End ;
  18:   ;
  19:   ;
  20:  !-- ARC PARAM -- ;
  21:  WAIT R[109]=0    ;
  22:  R[13]=6    ;
  23:  R[115]=1    ;
  24:  R[116]=1    ;
  25:  CALL WELD_PA    ;
  26:   ;
  27:  !-- AT RESUME JMP TO LABEL ;
  28:  IF ((R[151]=12)),JMP LBL[R[151]] ;
  29:  IF ((R[151]>100)),JMP LBL[R[151]] ;
  30:   ;
  31:   ;
  32:  !**************************** ;
  33:  !**** POSITION CALC & POINTS ;
  34:  !**************************** ;
  35:   ;
  36:  !* FUT 000 OR FUT 00-1 (PR[43-44) ;
  37:   ;
  38:  !***  POSITION CALC START ;
  39:  R[102]=(60+(R[95]*10)) ;
  40:   ;
  41:  !***  CONFIG CALC ;
  42:  R[103]=43    ;
  43:  IF (R[95]=(1)),R[103]=(45) ;
  44:   ;
  45:  CALL PRG_CALC1    ;
  46:   ;
  47:   ;
  48:  !Determind sensing point to use ;
  49:  R[101]=R[102]+1    ;
  50:  IF (R[254]=2),R[101]=R[102]+2 ;
  51:  IF (R[254]=3),R[101]=R[102]+2 ;
  52:  IF (R[254]=4),R[101]=R[102]+2 ;
  53:  IF (R[254]=7),R[101]=R[102]+2 ;
  54:   ;
  55:  !Robot distance ;
  56:  !-- WELDING  POINT 4 ;
  57:  R[141]=R[93]*(-1)    ;
  58:  R[135]=R[141]    ;
  59:  R[142]=0    ;
  60:  CALL PRG_CALC1    ;
  61:  PR[4]=PR[R[101]]    ;
  62:  PR[4,1]=PR[4,1]+R[141]    ;
  63:  PR[4,2]=PR[4,2]+R[142]    ;
  64:   ;
  65:  !Start Angle up point ;
  66:  !-- WELDING  POINT 5 ;
  67:  !Depending on OProfile flange ;
  68:  R[135]=R[93]*(-1)    ;
  69:  IF (R[253]=2),R[135]=(-110) ;
  70:  R[141]=R[135]    ;
  71:  R[142]=0    ;
  72:  CALL PRG_CALC1    ;
  73:  PR[5]=PR[R[101]]    ;
  74:  PR[5,1]=PR[5,1]+R[141]    ;
  75:  PR[5,2]=PR[5,2]+R[142]    ;
  76:   ;
  77:  !End Angle up point ;
  78:  !-- WELDING  POINT 6 ;
  79:  R[135]=R[135]-100    ;
  80:  R[141]=R[135]    ;
  81:  R[142]=0    ;
  82:  CALL PRG_CALC1    ;
  83:  PR[6]=PR[21]+PR[R[101]]    ;
  84:  PR[6,1]=PR[6,1]+R[141]    ;
  85:  PR[6,2]=PR[6,2]+R[142]    ;
  86:   ;
  87:  !Move ahead point ;
  88:  !-- WELDING  POINT 7 ;
  89:  R[141]=((R[93]+100)*(-1)) ;
  90:  R[142]=0    ;
  91:  CALL PRG_CALC1    ;
  92:  PR[7]=PR[6]    ;
  93:  PR[7,1]=PR[7,1]+R[141]    ;
  94:  PR[7,2]=PR[7,2]+R[142]    ;
  95:   ;
  96:  !**************************** ;
  97:  ! Call sub programs for Starting ;
  98:  !**************************** ;
  99:  LBL[12] ;
 100:   ;
 101:  IF (R[254]=(1)) THEN ;
 102:  CALL PB_COR1    ;
 103:  IF R[151]=12,JMP LBL[1000] ;
 104:  ENDIF ;
 105:   ;
 106:  IF (R[254]=(2) OR R[254]=3 OR R[254]=4 OR R[254]=7) THEN ;
 107:  CALL PB_EDGE1    ;
 108:  IF R[151]=12,JMP LBL[1000] ;
 109:  ENDIF ;
 110:   ;
 111:  IF (R[254]=5 OR R[254]=6 OR R[254]=8) THEN ;
 112:  CALL PB_MID1    ;
 113:  R[151]=104    ;
 114:  !if start of profile jump to end ;
 115:  IF R[221]=18,JMP LBL[1000] ;
 116:  ENDIF ;
 117:   ;
 118:  ! if one robot welds both sides ;
 119:  IF (R[290]<>0),JMP LBL[115] ;
 120:   ;
 121:  ! If not start of profile, jump t ;
 122:  IF (R[95]=1 OR R[70]=55),JMP LBL[115] ;
 123:   ;
 124:   ;
 125:  !**************************** ;
 126:  ! ***** WELDING ***** ;
 127:  ! ***** Sub points ***** ;
 128:  !**************************** ;
 129:  !---------------------------- ;
 130:  !-- WELDING MOTION P4 ;
 131:  LBL[104] ;
 132:  R[151]=104    ;
 133:   ;
 134:  !using current angles ;
 135:  PR[120]=LPOS    ;
 136:  PR[4,4]=PR[120,4]    ;
 137:  PR[4,5]=PR[120,5]    ;
 138:  PR[4,6]=PR[120,6]    ;
 139:   ;
 140:  !use different angles for endtyp ;
 141:  IF (R[254]=2 OR R[254]=4) THEN ;
 142:  PR[4,4]=PR[21,4]    ;
 143:  PR[4,5]=PR[21,5]    ;
 144:  PR[4,6]=PR[21,6]    ;
 145:  CALL PRG_CALC1    ;
 146:  ENDIF ;
 147:   ;
 148:  Weave Sine[1] ;
 149:  Track TAST[R[21]] ;
 150:  //Weld Start E1[1,R[111],E0] ;
 151:L PR[4] R[126]cm/min CNT100    ;
 152:  !Signal to start R3 ;
 153:  R[75]=1    ;
 154:   ;
 155:  !---------------------------- ;
 156:  !-- WELDING MOTION P5 ;
 157:  LBL[105] ;
 158:  R[151]=105    ;
 159:   ;
 160:  !using current angles ;
 161:  PR[5,4]=PR[4,4]    ;
 162:  PR[5,5]=PR[4,5]    ;
 163:  PR[5,6]=PR[4,6]    ;
 164:   ;
 165:  Weave Sine[1] ;
 166:  Track TAST[R[21]] ;
 167:  //Weld Start E1[1,R[111],E0] ;
 168:   ;
 169:  !if robot 3 is done -> skip ;
 170:  SKIP CONDITION R[75]=3    ;
 171:L PR[5] R[126]cm/min CNT100 Skip,LBL[1051]    ;
 172:  LBL[1051] ;
 173:  IF (R[75]=3),JMP LBL[999] ;
 174:   ;
 175:  ! if one robot welds both sides ;
 176:  IF (R[290]<>0),JMP LBL[115] ;
 177:   ;
 178:  !---------------------------- ;
 179:  !-- WELDING MOTION P6 ;
 180:  LBL[106] ;
 181:  R[151]=106    ;
 182:  Weave Sine[1] ;
 183:  Track TAST[R[21]] ;
 184:  //Weld Start E1[1,R[111],E0] ;
 185:   ;
 186:  !if robot 3 is done -> skip ;
 187:  SKIP CONDITION R[75]=3    ;
 188:L PR[6] R[126]cm/min CNT100 Skip,LBL[1061]    ;
 189:  LBL[1061] ;
 190:  IF (R[75]=3),JMP LBL[999] ;
 191:   ;
 192:   ;
 193:  LBL[107] ;
 194:  R[151]=107    ;
 195:  Weave Sine[1] ;
 196:  Track TAST[R[21]] ;
 197:  //Weld Start E1[1,R[111],E0] ;
 198:   ;
 199:  SKIP CONDITION R[75]=3    ;
 200:L PR[7] R[126]cm/min CNT100 Skip,LBL[999]    ;
 201:  JMP LBL[999] ;
 202:   ;
 203:  ! If End of profile  or rathole ;
 204:  ! Weld back to close point ;
 205:  LBL[115] ;
 206:  R[151]=115    ;
 207:  ! go in UF1 ;
 208:  UFRAME_NUM=1 ;
 209:   ;
 210:  !Signal to start R3 ;
 211:  R[75]=1    ;
 212:   ;
 213:  PR[130]=LPOS    ;
 214:  PR[15,7]=PR[130,7]    ;
 215:   ;
 216:  R[101]=R[102]+15    ;
 217:  PR[15]=PR[R[101]]    ;
 218:   ;
 219:  CALL PRG_CALC1    ;
 220:  PR[15,4]=PR[21,4]    ;
 221:  PR[15,5]=PR[21,5]    ;
 222:  PR[15,6]=PR[21,6]    ;
 223:   ;
 224:  !Use Gantry to weld back ;
 225:  IF R[294]=1,JMP LBL[999] ;
 226:   ;
 227:  Weave Sine[1] ;
 228:  Track TAST[R[21]] ;
 229:  Weld Start E1[1,R[111],E0] ;
 230:L PR[15] R[126]cm/min FINE    ;
 231:  Weave End[1] ;
 232:  Track End ;
 233:  Weld End E1[1,R[111]] ;
 234:   ;
 235:  LBL[999] ;
 236:   ;
 237:  R[151]=0    ;
 238:   ;
 239:  LBL[1000] ;
/POS
/END
