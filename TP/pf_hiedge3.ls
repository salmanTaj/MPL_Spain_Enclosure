/PROG  PF_HIEDGE3
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 3099;
CREATE		= DATE 10-08-05  TIME 14:56:58;
MODIFIED	= DATE 18-06-02  TIME 05:27:42;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 136;
MEMORY_SIZE	= 3611;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= *,*,1,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
  ARC : TRUE ; 
  ARC Welding Equipment : *,1,*,*,*;

MPAS ;
MPAS_NUM_PASSES        : 0;
MPAS_LAST_PASS         : 0;
MPAS_CURRENT_PASS      : 0;
MPAS_STATUS_PASS       : 0;
/MN
   1:  JMP LBL[555] ;
   2:  R[95]=1    ;
   3:  //R[153]=0    ;
   4:  LBL[555] ;
   5:  !************************** ;
   6:  !** WELD V/U ;
   7:  !** Close Corner ;
   8:  !************************** ;
   9:   ;
  10:  UFRAME_NUM=2 ;
  11:  UTOOL_NUM=1 ;
  12:   ;
  13:  !****************************** ;
  14:  !** I N I T I A L I S I N G  ** ;
  15:  !****************************** ;
  16:   ;
  17:  !****************************** ;
  18:  !--   ANGLE CALC  -- ;
  19:  !****************************** ;
  20:  !* FUT 000 OR FUT 00-1 (PR[43-44) ;
  21:   ;
  22:  !***  POSITION CALC START ;
  23:  R[105]=(60+(R[95]*10)) ;
  24:   ;
  25:  !***  CONFIG CALC ;
  26:  R[106]=43    ;
  27:  R[106]=(45) ;
  28:  !** -------------------  ** ;
  29:   ;
  30:   ;
  31:   ;
  32:  !****************************** ;
  33:  !**** POSITION CALC ;
  34:  !****************************** ;
  35:   ;
  36:  !-- SAFE End  POINT ;
  37:  R[145]=(-50)    ;
  38:  R[146]=75    ;
  39:  CALL PRG_CALC3    ;
  40:  R[104]=R[105]+4    ;
  41:  PR[35]=PR[28]+PR[R[104]]    ;
  42:  PR[35,1]=PR[35,1]+R[145]    ;
  43:  PR[35,2]=(PR[35,2]+R[146]) ;
  44:  PR[35,3]=PR[35,3]    ;
  45:  PR[35,5]=80    ;
  46:  !------- ;
  47:   ;
  48:  !------- ;
  49:   ;
  50:  !--  WELDING  POINT 5 ;
  51:  R[104]=R[105]+4    ;
  52:  PR[5]=PR[R[104]]+PR[28]    ;
  53:  PR[5,3]=PR[5,3]+4    ;
  54:  !Checking length of weld ;
  55:  !to avoid collision ;
  56:  R[137]=PR[5,3]-PR[1,3]    ;
  57:   ;
  58:  IF (R[137]<90),PR[5,5]=(80) ;
  59:  !------- ;
  60:   ;
  61:  !-- WELDING  POINT 6 ;
  62:  PR[6]=PR[5]    ;
  63:  R[145]=1.5    ;
  64:  R[146]=R[14]/1.1*(-1)    ;
  65:   ;
  66:  !if closing over side profile. sw ;
  67:  IF (R[266]=5) THEN ;
  68:  R[137]=R[145]    ;
  69:  R[145]=R[146]*(-1)    ;
  70:  R[146]=R[137]*(-1)    ;
  71:  ENDIF ;
  72:   ;
  73:   ;
  74:  CALL PRG_CALC3    ;
  75:  PR[6,1]=PR[6,1]+R[145]    ;
  76:  PR[6,2]=PR[6,2]+R[146]    ;
  77:   ;
  78:   ;
  79:  PR[6,3]=PR[6,3]+2    ;
  80:  PR[6,5]=80    ;
  81:  !------- ;
  82:   ;
  83:  !****************************** ;
  84:  !****    R E S U M I N G   **** ;
  85:  !****************************** ;
  86:   ;
  87:   ;
  88:  !-- AT RESUME JMP TO LABEL ;
  89:  IF R[153]>100,JMP LBL[R[153]] ;
  90:   ;
  91:  !ARCSENSOR START COUNT AT START ;
  92:  $TAST_PARAM[7].$SAMP_ST_Z=5 ;
  93:  !****************************** ;
  94:   ;
  95:   ;
  96:   ;
  97:  !****************************** ;
  98:  !--   W E L D I N G  -- ;
  99:   ;
 100:  !-- 1. WELDING MOTION ;
 101:   ;
 102:   ;
 103:  LBL[210] ;
 104:  R[153]=210    ;
 105:   ;
 106:  Weave Sine[3] ;
 107:  Track TAST[R[23]] ;
 108:  Weld Start E2[1,R[112],E0] ;
 109:L PR[5] R[127]cm/min CNT100    ;
 110:   ;
 111:  !---------------------------- ;
 112:   ;
 113:  !-- 2. WELDING MOTION ;
 114:   ;
 115:  LBL[240] ;
 116:  R[153]=240    ;
 117:   ;
 118:  Weave Sine[3] ;
 119:  Track TAST[R[23]] ;
 120:  Weld Start E2[1,R[112],E0] ;
 121:  Weave End ;
 122:L PR[6] R[127]cm/min FINE DB   15.0mm,GO[13]=10    ;
 123:  Track End ;
 124:  Weld End E2[1,R[112]] ;
 125:   ;
 126:  LBL[241] ;
 127:  R[153]=241    ;
 128:L PR[35] 250mm/sec FINE    ;
 129:   ;
 130:   ;
 131:  !---------------------------- ;
 132:   ;
 133:   ;
 134:  !****************************** ;
 135:  !--   E N D I N G    P R G.  -- ;
 136:   ;
/POS
/END
