/PROG  PF_HISTOP1
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 2867;
CREATE		= DATE 10-08-05  TIME 14:56:58;
MODIFIED	= DATE 18-05-26  TIME 09:30:48;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 125;
MEMORY_SIZE	= 3295;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
  ARC : TRUE ; 
  ARC Welding Equipment : 1,*,*,*,*;

MPAS ;
MPAS_NUM_PASSES        : 0;
MPAS_LAST_PASS         : 0;
MPAS_CURRENT_PASS      : 0;
MPAS_STATUS_PASS       : 0;
/MN
   1:  JMP LBL[555] ;
   2:  R[95]=1    ;
   3:  //R[151]=0    ;
   4:  LBL[555] ;
   5:  !************************** ;
   6:  !** WELD V/U ;
   7:  !** Close Corner ;
   8:  !************************** ;
   9:   ;
  10:  UFRAME_NUM=2 ;
  11:  UTOOL_NUM=1 ;
  12:   ;
  13:  !****************************** ;
  14:  !** I N I T I A L I S I N G  ** ;
  15:  !****************************** ;
  16:   ;
  17:  !****************************** ;
  18:  !--   ANGLE CALC  -- ;
  19:  !****************************** ;
  20:  !* FUT 000 OR FUT 00-1 (PR[43-44) ;
  21:   ;
  22:  !***  POSITION CALC START ;
  23:  R[102]=(60+(R[95]*10)) ;
  24:   ;
  25:  !***  CONFIG CALC ;
  26:  R[103]=43    ;
  27:  R[103]=(45) ;
  28:  !** -------------------  ** ;
  29:   ;
  30:   ;
  31:   ;
  32:  !****************************** ;
  33:  !**** POSITION CALC ;
  34:  !****************************** ;
  35:   ;
  36:  !-- SAFE End  POINT ;
  37:  R[141]=(-50)    ;
  38:  R[142]=75    ;
  39:  CALL PRG_CALC1    ;
  40:  R[101]=R[102]+4    ;
  41:  PR[35]=PR[28]+PR[R[101]]    ;
  42:  PR[35,1]=PR[35,1]+R[141]    ;
  43:  PR[35,2]=(PR[35,2]+R[142]) ;
  44:  PR[35,3]=PR[35,3]    ;
  45:  PR[35,5]=75    ;
  46:  !------- ;
  47:   ;
  48:  !------- ;
  49:   ;
  50:  !--  WELDING  POINT 5 ;
  51:  R[101]=R[102]+4    ;
  52:  PR[5]=PR[R[101]]+PR[28]    ;
  53:  PR[5,3]=PR[5,3]-40    ;
  54:  PR[5,5]=90    ;
  55:  !if flange ;
  56:  IF ((R[253]=2) AND (PR[5,3]>125)),PR[5,5]=100 ;
  57:  !------- ;
  58:   ;
  59:  !-- WELDING  POINT 6 ;
  60:  PR[6]=PR[R[101]]+PR[28]    ;
  61:  PR[6,3]=PR[6,3]-2    ;
  62:  PR[6,5]=100    ;
  63:  !low profiles ;
  64:  IF (PR[5,3]-PR[3,3]<30) THEN ;
  65:  PR[6,5]=90    ;
  66:  PR[5]=PR[6]    ;
  67:  ENDIF ;
  68:  !------- ;
  69:   ;
  70:  !****************************** ;
  71:  !****    R E S U M I N G   **** ;
  72:  !****************************** ;
  73:   ;
  74:   ;
  75:  !-- AT RESUME JMP TO LABEL ;
  76:  IF R[151]>100,JMP LBL[R[151]] ;
  77:   ;
  78:  !ARCSENSOR START COUNT AT START ;
  79:  $TAST_PARAM[7].$SAMP_ST_Z=5 ;
  80:  !****************************** ;
  81:   ;
  82:   ;
  83:   ;
  84:  !****************************** ;
  85:  !--   W E L D I N G  -- ;
  86:   ;
  87:  !-- 1. WELDING MOTION ;
  88:   ;
  89:   ;
  90:  LBL[210] ;
  91:  R[151]=210    ;
  92:   ;
  93:  Weave Sine[1] ;
  94:  Track TAST[R[21]] ;
  95:  Weld Start E1[1,R[111],E0] ;
  96:L PR[5] R[126]cm/min CNT100    ;
  97:   ;
  98:  !---------------------------- ;
  99:   ;
 100:  !-- 2. WELDING MOTION ;
 101:   ;
 102:  LBL[240] ;
 103:  R[151]=240    ;
 104:   ;
 105:  Weave Sine[1] ;
 106:  Track TAST[R[21]] ;
 107:  Weld Start E1[1,R[111],E0] ;
 108:   ;
 109:L PR[6] R[126]cm/min FINE    ;
 110:   ;
 111:  LBL[241] ;
 112:  R[151]=241    ;
 113:  Weave End ;
 114:  Track End ;
 115:  Weld End E1[1,R[111]] ;
 116:   ;
 117:L PR[35] 250mm/sec FINE    ;
 118:   ;
 119:   ;
 120:  !---------------------------- ;
 121:   ;
 122:   ;
 123:  !****************************** ;
 124:  !--   E N D I N G    P R G.  -- ;
 125:   ;
/POS
/END
